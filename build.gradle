
buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1+"
    }
}

plugins {
    id "de.undercouch.download" version "3.4.3"
}

def platformReleases = [
        [
                "name": "mac",
                "platform": "osx64",
        ],
        [
                "name": "linux",
                "platform": "x86_64-linux-gnu",
        ],
]
def bitcoinVersion = "0.19.0.1"

allprojects {
    apply plugin: 'java'
    //apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'

    group 'org.cryptocurrency-testing'
    version = "$bitcoinVersion-fromsrc"
    archivesBaseName = "embedded-bitcoind-binaries"
    sourceCompatibility = 1.6

    repositories {
        jcenter()
    }

    configurations {
        bundles
    }
}



project(':bitcoind-artifacts') {
    task "build-linux"(group: "build (linux)", type: Exec) {
        workingDir "$buildDir"
        executable "../../build-x86_64-linux-gnu.sh"
        commandLine "../../build-x86_64-linux-gnu.sh", "v0.19.0.1-quiet-zmq"
        outputs.dir(file("$buildDir/bitcoin"))
    }
    task "copy-bin-linux"(group: "build (linux)", type: Copy) {
        dependsOn "build-linux"
        from("$buildDir/bitcoin/src") {
            include 'bitcoind'
        }
        into "$buildDir/linux/x86_64-linux-gnu/bin"
    }
    task "build-jar-linux"(group: "build (linux)", type: Jar) {
        dependsOn "copy-bin-linux"
        from file("$buildDir/linux")
        appendix = "x86_64-linux-gnu"
    }

    task "build-mac"(group: "build (mac)", type: Exec) {
        workingDir "$buildDir"
        executable "../../build-osx64.sh"
        commandLine "../../build-osx64.sh", "v0.19.0.1-quiet-zmq"
        outputs.dir(file("$buildDir/bitcoin"))
    }
    task "copy-bin-mac"(group: "build (mac)", type: Copy) {
        dependsOn "build-mac"
        from("$buildDir/bitcoin/src") {
            include 'bitcoind'
        }
        into "$buildDir/mac/osx64/bin"
    }
    task "build-jar-mac"(group: "build (mac)", type: Jar) {
        dependsOn "copy-bin-mac"
        from file("$buildDir/mac")
        appendix = "osx64"
    }
    artifacts.add('bundles', tasks.getByName("build-jar-mac"))
    artifacts.add('bundles', tasks.getByName("build-jar-linux"))
}

subprojects {
    task sourcesJar(type: Jar, dependsOn: classes) {
        from sourceSets.main.allSource
        classifier = 'sources'
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        from javadoc.destinationDir
        classifier = 'javadoc'
    }

    publishing {
        publications {
            configurations.bundles.artifacts.all { archive ->
                def publicationName = archive.archiveTask.name - 'Jar'
                "$publicationName"(MavenPublication) {
                    artifactId "${archive.name}"
                    configurePom(pom, artifactId, 'A lightweight bundle of bitcoind with reduced size')

                    artifact archive
                    artifact sourcesJar
                    artifact javadocJar
                }
            }
        }
    }
}

publishing {
    publications {
        bom(MavenPublication) {
            artifactId 'embedded-bitcoind-binaries-bom'
            configurePom(pom, artifactId, 'Bill of Materials')

            pom.withXml {
                def root = asNode()
                root.children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST

                    dependencyManagement {
                        dependencies {
                            project.subprojects.collectMany { it.configurations.bundles.artifacts }

                                    .each { archive ->
                                        dependency {
                                            groupId "${project.group}"
                                            artifactId "${archive.name}"
                                            version "${project.version}"

                                            if (!archive.name.contains('amd64') || archive.name.contains('lite')) {
                                                optional 'true'
                                            }
                                        }
                                    }
                        }
                    }
                }
            }
        }
    }
}

allprojects {

    bintray {
        user = project.hasProperty('bintray.user') ? project.property('bintray.user') : System.getenv('BINTRAY_USER')
        key = project.hasProperty('bintray.apiKey') ? project.property('bintray.apiKey') : System.getenv('BINTRAY_API_KEY')
        publications = project.publishing.publications.findAll().collect { it.name }
        pkg {
            repo = 'embedded-bitcoind-binaries'
            userOrg = 'cryptocurrency-testing'
            name = 'embedded-bitcoind-binaries'
            desc = 'Embedded bitcoind binaries for tests'
            licenses = ['MIT']
            vcsUrl = 'https://github.com/cryptocurrency-testing/embedded-bitcoind-binaries.git'
            version {
                name = project.version
                gpg {
                    sign = true //Determines whether to GPG sign the files. The default is false
                    passphrase = project.hasProperty('bintray.pgpPassphrase') ? project.property('bintray.pgpPassphrase') : System.getenv('BINTRAY_PGP_PASSPHRASE')
                }
            }
        }
    }

    task install(group: 'publishing') {}

    configurations.bundles.artifacts.all { archive ->
        def publicationName = archive.archiveTask.name - 'Jar'

        jar.dependsOn "${archive.archiveTask.name}"
        //test.dependsOn "test${archive.archiveTask.name.capitalize()}"
        install.dependsOn "publish${publicationName.capitalize()}PublicationToMavenLocal"
    }

    task uploadArchives(group: 'publishing') {
        dependsOn bintrayUpload
    }

    tasks.whenTaskAdded { task ->
        if (task.name == 'publishBomPublicationToMavenLocal') {
            install.dependsOn task
        }
    }
}

def configurePom(pom, artifact, desc) {
    pom.withXml {
        def root = asNode()

        root.children().last() + {
            resolveStrategy = Closure.DELEGATE_FIRST

            name artifact
            description desc
            url 'https://github.com/cryptocurrency-testing/embedded-bitcoind-binaries'

            scm {
                connection 'scm:git:git://github.com/cryptocurrency-testing/embedded-bitcoind-binaries.git'
                developerConnection 'scm:git:ssh://github.com:cryptocurrency-testing/embedded-bitcoind-binaries.git'
                url 'https://github.com/cryptocurrency-testing/embedded-bitcoind-binaries/tree/master'
            }

            licenses {
                license {
                    name 'The MIT Licence'
                    url 'https://opensource.org/licenses/MIT'
                }
            }

            developers {
                developer {
                    name 'James Hilliard'
                    email 'james.hilliard1@gmail.com'
                }
                developer {
                    name 'Thomas Kerin'
                    email 'me@thomaskerin.io'
                }
            }
        }
    }
}

class LazyExec extends AbstractExecTask<LazyExec> {
    LazyExec() {
        super(LazyExec.class)
    }

    @Override
    LazyExec commandLine(Object... arguments) {
        return super.commandLine(arguments.collect { argument ->
            if (argument instanceof Closure) {
                Closure closure = (Closure) argument;
                return new Object() {
                    @Override
                    String toString() {
                        return closure()
                    }
                }
            } else {
                return argument
            }
        }) as LazyExec
    }
}
